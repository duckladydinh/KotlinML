buildscript {
    ext.kotlinVersion = "1.3.61"
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'maven-publish'
    id 'java-library'

    // Fat jar
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

repositories {
    jcenter()

    flatDir {
        dir 'libs/lightgbm'
        dir 'libs/lbfgsb'
    }

    maven { url "https://dl.bintray.com/kyonifer/maven" }
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    api name: "lightgbmlib_linux"
    api name: "lbfgsblib_linux" // sudo apt install libgfortran-9-dev for this

    api "com.kyonifer:koma-core-ejml:0.12"
    api "de.mpicbg.scicomp:krangl:0.11"

    api 'com.google.flogger:flogger:0.4'
    api 'com.google.flogger:flogger-system-backend:0.4'

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}

group = 'thuan.handsome'
version = '1.0-SNAPSHOT'

test {
    useJUnitPlatform()
}

//noinspection GrUnresolvedAccess
[clean, cleanTest].each {
    //noinspection GrUnresolvedAccess
    it.doFirst {
        println "Cleaning previous logs!"
        delete "logs"
    }
}

compileKotlin {
    //noinspection GrUnresolvedAccess
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    //noinspection GrUnresolvedAccess
    kotlinOptions.jvmTarget = "11"
}

jar {
    dependsOn shadowJar
    enabled = false
}

project.configurations.api.canBeResolved = true

shadowJar {
    archiveClassifier.set(null)
    configurations = [project.configurations.api]
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        //noinspection GrUnresolvedAccess
        mavenProject(MavenPublication) {
            //noinspection GrUnresolvedAccess
            from components.java
            //noinspection GrUnresolvedAccess
            groupId project.group
            //noinspection GrUnresolvedAccess
            artifactId project.name
            version project.version

            //noinspection GrUnresolvedAccess
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}
