buildscript {
    ext {
        kotlinVersion = '1.3.61'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

apply plugin: 'kotlin'

repositories {
    jcenter()

    flatDir {
        dir 'libs/lightgbm'
        dir 'libs/lbfgsb'
    }

    maven { url "https://dl.bintray.com/kyonifer/maven" }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation "com.kyonifer:koma-core-ejml:0.12"
    implementation "com.kyonifer:koma-plotting:0.12"
    implementation "de.mpicbg.scicomp:krangl:+"

    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.0' // logging implementation
    implementation "io.github.microutils:kotlin-logging:1.7.7" // logging api

    implementation name: "lightgbmlib_linux"
    implementation name: "lbfgsblib_linux" // sudo apt install libgfortran-9-dev for this

    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlinVersion"
}

group = 'thuan.handsome'
version = '1.0-SNAPSHOT'

//noinspection GrUnresolvedAccess
[clean, cleanTest].each {
    //noinspection GrUnresolvedAccess
    it.doFirst {
        println "Cleaning previous logs!"
        delete "logs"
    }
}

compileKotlin {

    //noinspection GrUnresolvedAccess
    kotlinOptions {
        jvmTarget = "11"
    }
}
